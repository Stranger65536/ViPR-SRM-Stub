buildscript {
    ext {
        springBootVersion = '1.5.8.RELEASE'
        licensePluginVersion = '0.14.0'
    }
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://repo.spring.io/snapshot' }
        maven { url 'https://repo.spring.io/milestone' }
        maven { url 'http://repository.pentaho.org/artifactory/repo' }
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:${licensePluginVersion}")
    }
}

apply plugin: 'org.springframework.boot'
apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'com.github.hierynomus.license'

group 'com.emc'
version '1.0-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

license {
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
    header project.rootDir.toPath().resolve('LICENSE').toFile()
    strictCheck true
    ignoreFailures = true
    excludes(['**/*.txt',
              '**/*.json'])
}

findbugs {
    effort = "max"
    reportLevel = "low"
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
        includeFilter = project.rootDir.toPath().resolve('findbugs').resolve('findbugs-include.xml').toFile()
        excludeFilter = project.rootDir.toPath().resolve('findbugs').resolve('findbugs-exclude.xml').toFile()
    }
}

build.dependsOn(licenseFormatMain)
test.dependsOn(licenseFormatTest)

configurations {
    all*.exclude module: 'spring-boot-starter-logging'
    all*.exclude group: 'javax.servlet', module: 'servlet-api'
    providedRuntime
}

project.ext.springBootVersion = '1.5.8.RELEASE'
project.ext.jacksonVersion = '2.9.1'
project.ext.junitVersion = '4.12'
project.ext.hamcrestVersion = '1.3'
project.ext.guavaVersion = '23.0'

bootRun {
    //noinspection GroovyAssignabilityCheck
    systemProperties = System.properties
}

test {
    //noinspection GroovyAssignabilityCheck
    systemProperties = System.properties
}

dependencies {
    compile(
            [group: 'org.springframework.boot', name: 'spring-boot-starter', version: project.springBootVersion],
            [group: 'org.springframework.boot', name: 'spring-boot-starter-log4j2', version: project.springBootVersion],
            [group: 'org.springframework.boot', name: 'spring-boot-starter-web-services', version: project.springBootVersion],

            [group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: project.jacksonVersion],
            [group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: project.jacksonVersion],
            [group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: project.jacksonVersion],

            [group: 'com.google.guava', name: 'guava', version: project.guavaVersion],
    )
    testCompile(
            [group: 'junit', name: 'junit', version: project.junitVersion],
            [group: 'org.hamcrest', name: 'hamcrest-all', version: project.hamcrestVersion],
            [group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: project.springBootVersion],
    )
    runtimeOnly(
            [group: 'org.springframework.boot', name: 'spring-boot-starter-tomcat', version: project.springBootVersion],
    )
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}